import com.android.build.gradle.AppExtension
import org.ajoberstar.grgit.Grgit

plugins {
    id 'net.linguica.maven-settings' version '0.5'
    id 'org.ajoberstar.grgit' version '1.7.2'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

group 'mil.nga.giat.mage'
archivesBaseName = 'mage-android'
version '6.0.4'
ext {
    versionCode = 52
    sourceRefspec = Grgit.open().head().id
}

def serverURL = hasProperty('SERVER_URL') ? SERVER_URL : ''
def googleMapsApiReleaseKey = hasProperty('RELEASE_MAPS_API_KEY') ? RELEASE_MAPS_API_KEY : ''
def googleMapsApiDebugKey = hasProperty('DEBUG_MAPS_API_KEY') ? DEBUG_MAPS_API_KEY : ''
def signedApkPath = hasProperty('SIGNED_APK_PATH') ? SIGNED_APK_PATH : "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-release.apk"
def signedDebugApkPath = hasProperty('SIGNED_DEBUG_APK_PATH') ? SIGNED_DEBUG_APK_PATH : "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-debug.apk"
String pin = hasProperty('mage.pinned_cert') ? getProperty("mage.pinned_cert") : ''

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    flavorDimensions "dimensionless"

    defaultConfig {
        applicationId "mil.nga.giat.mage"
        versionName project.version
        versionCode project.versionCode as int
        minSdkVersion 19
        targetSdkVersion 26
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resValue "string", "serverURLDefaultValue", serverURL
        resValue "string", "recentMapXYZDefaultValue", "263.0,40.0,3"
        resValue "bool", "isWearBuildDefaultValue", "false"
        resValue "string", "source_refspec", project.sourceRefspec
        resValue "string", "applicationId", applicationId
        manifestPlaceholders source_refspec: project.sourceRefspec
        buildConfigField "String", "SOURCE_REFSPEC", $/"${project.sourceRefspec}"/$
        testInstrumentationRunner "com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiReleaseKey
            resValue "string", "serverPin", ""
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiDebugKey
            resValue "string", "applicationId", defaultConfig.applicationId + applicationIdSuffix
            resValue "string", "serverPin", pin
        }
    }

    productFlavors {
        defaults {
            // if you build without a flavor gradle will build all flavors
            // define this flavor to provide a flavor we can build with no overrides
            dimension "dimensionless"
        }
        wear {
            dimension "dimensionless"
            resValue "bool", "isWearBuildDefaultValue", "true"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
    }
}

def configurePom(pom) {
    pom.withXml {
        // for dependencies and exclusions
        def dependenciesNode = asNode().appendNode('dependencies')
        project.configurations.compile.allDependencies.each { Dependency dep ->
            if (dep instanceof SelfResolvingDependency) {
                // apparently the files() dependency results in a SelfResolvingDependency - skip it
                return
            }

            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dep.group)
            dependencyNode.appendNode('artifactId', dep.name)
            dependencyNode.appendNode('version', dep.version)

            // for exclusions
            if (dep.excludeRules.size() > 0) {
                def exclusions = dependencyNode.appendNode('exclusions')
                dep.excludeRules.each { ExcludeRule ex ->
                    def exclusion = exclusions.appendNode('exclusion')
                    exclusion.appendNode('groupId', ex.group)
                    exclusion.appendNode('artifactId', ex.module)
                }
            }
        }
    }
}

publishing {
    publications {
        defaultsApk(MavenPublication) {
            artifactId archivesBaseName
            pom.packaging 'apk'
            configurePom(pom)
            artifact "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-release-unsigned.apk", {
                classifier 'unsigned'
            }
            if (file(signedApkPath).exists()) {
                artifact signedApkPath
            }
            if (file(signedDebugApkPath).exists()) {
                artifact signedDebugApkPath, {
                    classifier 'debug'
                }
            }
            if (project.hasProperty('SKIP_DEBUG_SIGNING')) {
                artifact "${project.buildDir}/outputs/apk/${archivesBaseName}-defaults-debug-unsigned.apk", {
                    classifier 'debug-unsigned'
                }
            }
        }
    }
}

repositories {
    def buildRepo = maven {
        name 'build'
        url project.properties['repositories.build.url'] ?: "${buildDir}/m2"
    }
    publishing.repositories.add buildRepo

    if (project.hasProperty('repositories.shared.name')) {
        def sharedRepo = maven {
            name project.properties['repositories.shared.name']
            url project.properties['repositories.shared.url']
        }
        publishing.repositories.add sharedRepo
    }

    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    support_library_version = '26.1.0'
    google_play_services_version = '10.2.1'
    architecture_components_version = '1.1.0'
}

dependencies {

    // this block allows the mage-android-wear-bridge to be pulled in dynamically when that flavor is built
    gradle.startParameter.taskRequests.each { taskRequest ->
        taskRequest.args.each { taskName ->
            if (taskName.toLowerCase().contains("wear")) {
                //wearCompile project(':bridge') // uncomment me to build wearable locally
                wearCompile "mil.nga.giat.mage.wearable:bridge:1.0.0" // uncomment me to build wearable.  comment me if building wearable locally.
            }
        }
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:support-v13:${support_library_version}"
    implementation "com.android.support:animated-vector-drawable:${support_library_version}"
    implementation "com.android.support:mediarouter-v7:${support_library_version}"
    implementation "com.android.support:recyclerview-v7:${support_library_version}"
    implementation "com.android.support:appcompat-v7:${support_library_version}"
    implementation "com.android.support:design:${support_library_version}"
    implementation "com.android.support:cardview-v7:${support_library_version}"
    implementation "com.android.support:preference-v7:${support_library_version}"
    implementation "com.android.support:preference-v14:${support_library_version}"
    implementation "com.google.android.gms:play-services-maps:${google_play_services_version}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'
    implementation "android.arch.lifecycle:common-java8:${architecture_components_version}"
    implementation "android.arch.lifecycle:extensions:${architecture_components_version}"
    implementation "android.arch.lifecycle:livedata:${architecture_components_version}"

    implementation project(':sdk') // uncomment me to build locally, and see top-level build.gradle
//    compile 'mil.nga.giat.mage:mage-android-sdk:6.0.3' // comment me to build locally
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'mil.nga.geopackage.map:geopackage-android-map:1.4.1'
    implementation 'org.ocpsoft.prettytime:prettytime:3.2.5.Final'
    implementation 'com.github.bumptech.glide:glide:3.6.1'
    implementation 'com.github.bumptech.glide:okhttp-integration:1.3.1@aar'
    implementation 'com.github.woxthebox:draglistview:1.5.1'
    implementation 'com.google.android:flexbox:0.3.0'
    implementation 'com.nulab-inc:zxcvbn:1.2.3'
    implementation (name:'mgrs-0.0.2-release', ext:'aar')
    implementation files('libs/GeocoderPlus.jar')

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    testImplementation 'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1'

    androidTestImplementation "com.android.support:support-annotations:${support_library_version}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.github.tmurakami:dexopener:0.9.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.mockito:mockito-android:2.13.0'
}

task androidArtifactVersion {
    group = 'android'
    description = 'Print the application ID, version code, and version name this build will produce in AndroidManifest.xml'
    doLast {
        AppExtension appExt = project.extensions.android
        println "applicationId: ${appExt.defaultConfig.applicationId}"
        println "versionCode: ${appExt.defaultConfig.versionCode}"
        println "versionName: ${appExt.defaultConfig.versionName}"
    }
}

configure extensions.android, {
    if (project.hasProperty('RELEASE_STORE_FILE') && file(project.getProperty('RELEASE_STORE_FILE')).exists()) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
    else {
        println 'no release keystore configured'
    }

    if (project.hasProperty('SKIP_DEBUG_SIGNING')) {
        buildTypes {
            debug {
                signingConfig null
            }
        }
    }
    else if (project.hasProperty('DEBUG_STORE_FILE') && file(project.getProperty('DEBUG_STORE_FILE')).exists()) {
        signingConfigs {
            debug {
                storeFile file(DEBUG_STORE_FILE)
                storePassword DEBUG_STORE_PASSWORD
                keyAlias DEBUG_KEY_ALIAS
                keyPassword DEBUG_KEY_PASSWORD
            }
        }
    }
}
